package kwic
public
	with Data_Model;
	with Base_Types;

	system kwic_sys
	end kwic_sys;

	system implementation kwic_sys.i
		subcomponents
			cpu0: processor genuineintel;
			mem: memory mem;
			input: process input_pr.i;
			circular: process circular_pr.i;
			alpha: process alpha_pr.i;
			output: process output_pr.i;
			ibus: virtual bus interthread;
			keyboard: device keyboard;
		connections
			input_circular: port input.lines_out -> circular.lines_in;
			circular_alpha: port circular.lines_out -> alpha.lines_in;
			alpha_output: port alpha.lines_out -> output.lines_in;
			input_circular_notify: port input.notify_circular -> circular.rcv_callback;
			circular_alpha_notify: port circular.notify_alpha -> alpha.rcv_callback;
			alpha_output_notify: port alpha.notify_output -> output.rcv_callback;
		flows
			ic_lines_etef: end to end flow input.foi -> input_circular -> circular.fpc;
			ca_lines_etef: end to end flow circular.foc -> circular_alpha -> alpha.fpa;
			ao_lines_etef: end to end flow alpha.foa -> alpha_output -> output.fpo;
			ic_notify_etef: end to end flow input.fni -> input_circular_notify -> circular.fcc;
			ca_notify_etef: end to end flow circular.fnc -> circular_alpha_notify -> alpha.fca;
			ao_notify_etef: end to end flow alpha.fna -> alpha_output_notify -> output.fco;
			
		properties
			Actual_Processor_Binding => (reference (cpu0))
			applies to
			input, circular, alpha, output;
			Actual_Memory_Binding => (reference (mem))
			applies to
			input, circular, alpha, output;
	end kwic_sys.i;

	process input_pr
		features
			lines_out: out data port string_list;
			notify_circular: out event port;
		flows
			foi: flow source lines_out;
			fni: flow source notify_circular;
	end input_pr;

	process implementation input_pr.i
		subcomponents
			process_thr: thread input_th;
			notify_thr: thread input_notify_th;
			input_buffer: data buffer.i;
		connections
			c0: port process_thr.lines_out -> lines_out;
			c1: port notify_thr.notify_circular -> notify_circular;
		flows
			foi: flow source process_thr.fo -> c0 -> lines_out;
			fni: flow source notify_thr.fn -> c1 -> notify_circular;
	end input_pr.i;

	process circular_pr
		features
			lines_in: in data port string_list;
			lines_out: out data port string_list;
			notify_alpha: out event port;
			rcv_callback: in event port;
		flows
			foc: flow source lines_out;
			fnc: flow source notify_alpha;
			fcc: flow sink rcv_callback;
			fpc: flow sink lines_in;
	end circular_pr;

	process implementation circular_pr.i
		subcomponents
			process_thr: thread circular_th;
			notify_thr: thread circular_notify_th;
			callback_thr: thread circular_callback_th;
			circular_buffer: data buffer.i;
		connections
			c0: port process_thr.lines_out -> lines_out;
			c1: port lines_in -> callback_thr.input_data_in;
			c2: port notify_thr.notify_alpha -> notify_alpha;
			c3: port rcv_callback -> callback_thr.on_input_ready;
			c4: port callback_thr.input_data_fwd -> process_thr.lines_in;
		flows
			fpc: flow sink lines_in -> c1 -> callback_thr.fp -> c4 -> process_thr.fi;
			foc: flow source process_thr.fo -> c0 -> lines_out;
			fnc: flow source notify_thr.fn -> c2 -> notify_alpha;
			fcc: flow sink rcv_callback -> c3 -> callback_thr.fc;
	end circular_pr.i;

	process alpha_pr
		features
			lines_in: in data port string_list;
			lines_out: out data port string_list;
			notify_output: out event port;
			rcv_callback: in event port;
		flows
			fpa: flow sink lines_in;
			foa: flow source lines_out;
			fna: flow source notify_output;
			fca: flow sink rcv_callback;
	end alpha_pr;

	process implementation alpha_pr.i
		subcomponents
			process_thr: thread alpha_th;
			notify_thr: thread alpha_notify_th;
			callback_thr: thread alpha_callback_th;
			alpha_buffer: data buffer.i;
		connections
			c0: port process_thr.lines_out -> lines_out;
			c1: port lines_in -> callback_thr.circular_data_in;
			c2: port notify_thr.notify_output -> notify_output;
			c3: port rcv_callback -> callback_thr.on_circular_ready;
			c4: port callback_thr.circular_data_fwd -> process_thr.lines_in;
		flows
			fpa: flow sink lines_in -> c1 -> callback_thr.fp -> c4 -> process_thr.fi;
			foa: flow source process_thr.fo -> c0 -> lines_out;
			fna: flow source notify_thr.fn -> c2 -> notify_output;
			fca: flow sink rcv_callback -> c3 -> callback_thr.fc;
	end alpha_pr.i;

	process output_pr
		features
			lines_in: in data port string_list;
			rcv_callback: in event port;
		flows
			fpo: flow sink lines_in;
			fco: flow sink rcv_callback;
	end output_pr;

	process implementation output_pr.i
		subcomponents
			process_thr: thread output_th;
			callback_thr: thread output_callback_th;
			output_buffer: data buffer.i;
		connections
			c0: port lines_in -> callback_thr.alpha_data_in;
			c1: port rcv_callback -> callback_thr.on_alpha_ready;
			c2: port callback_thr.alpha_data_fwd -> process_thr.lines_in;
		flows
			fpo: flow sink lines_in -> c0 -> callback_thr.fp -> c2 -> process_thr.fi;
			fco: flow sink rcv_callback -> c1 -> callback_thr.fc;
	end output_pr.i;

	thread input_th
		features
			lines_out: out data port string_list;
		flows
			fo: flow source lines_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 1ms .. 3ms;
	end input_th;
	
	thread input_notify_th
		features
			notify_circular: out event port;
		flows
			fn: flow source notify_circular;
		properties
			Dispatch_Protocol => Aperiodic;
	end input_notify_th;

	thread circular_th
		features
			lines_in: in data port string_list;
			lines_out: out data port string_list;
		flows
			fi: flow sink lines_in;
			fo: flow source lines_out;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 2ms .. 8ms;
	end circular_th;
	
	thread circular_notify_th
		features
			notify_alpha: out event port;
		flows
			fn: flow source notify_alpha;
		properties
			Dispatch_Protocol => Aperiodic;
	end circular_notify_th;
	
	thread circular_callback_th
		features
			on_input_ready: in event port;
			input_data_in: in data port string_list;
			input_data_fwd: out data port string_list;
		flows
			fc: flow sink on_input_ready;
			fp: flow path input_data_in -> input_data_fwd;
		properties
			Dispatch_Protocol => Aperiodic;
	end circular_callback_th;

	thread alpha_th
		features
			lines_in: in data port string_list;
			lines_out: out data port string_list;
		flows
			fi: flow sink lines_in;
			fo: flow source lines_out;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 2ms .. 10ms;
	end alpha_th;
	
	thread alpha_notify_th
		features
			notify_output: out event port;
		flows
			fn: flow source notify_output;
		properties
			Dispatch_Protocol => Aperiodic;
	end alpha_notify_th;
	
	thread alpha_callback_th
		features
			on_circular_ready: in event port;
			circular_data_in : in data port string_list;
			circular_data_fwd: out data port string_list;
		flows
			fc: flow sink on_circular_ready;
			fp: flow path circular_data_in -> circular_data_fwd;
		properties
			Dispatch_Protocol => Aperiodic;
	end alpha_callback_th;
	
	thread output_th
		features
			lines_in: in data port string_list;
		flows
			fi: flow sink lines_in;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 1ms .. 3ms;
	end output_th;
	
	thread output_callback_th
		features
			on_alpha_ready: in event port;
			alpha_data_in : in data port string_list;
			alpha_data_fwd: out data port string_list;
		flows
			fc: flow sink on_alpha_ready;
			fp: flow path alpha_data_in -> alpha_data_fwd;
		properties
			Dispatch_Protocol => Aperiodic;
	end output_callback_th;

	data string_list
	end string_list;

	data buffer
	end buffer;
	
	data implementation buffer.i
		subcomponents
			blocking_queue: data string_list;
		properties
			Data_Model::Data_Representation => Struct;
			Concurrency_Control_Protocol => Semaphore;
	end buffer.i;
	
	data implementation string_list.i
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier(Base_Types::String));
	end string_list.i;
	
	device keyboard
		features
			ibus: requires virtual bus access interthread.i;
			raw_input: out data port Base_Types::Character;
	end keyboard;

	processor genuineintel
		features
			ibus: requires virtual bus access interthread.i;
	end genuineintel;

	memory mem
		features
			ibus: requires virtual bus access interthread.i;
	end mem;

	virtual bus interthread
		properties
			Transmission_Type => push;
	end interthread;
	
	virtual bus implementation interthread.i
	end interthread.i;
	
end kwic;