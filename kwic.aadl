package kwic
public
	system kwic_sys
	end kwic_sys;
	
	system implementation kwic_sys.i
		subcomponents
			cpu0 : processor genuineintel;
			mem : memory mem;
			input : process input_pr.i;
			circular : process circular_pr.i;
			alpha : process alpha_pr.i;
			output : process output_pr.i;
		connections
			input_circular : port input.lines_out -> circular.lines_in;
			circular_alpha : port circular.lines_out -> alpha.lines_in;
			alpha_output : port alpha.lines_out -> output.lines_in;
		flows
			etef : end to end flow input.fi -> input_circular -> circular.fc -> 
			circular_alpha -> alpha.fa -> alpha_output -> output.fo
			{latency => 10ms..15ms;};
		properties
			Actual_Processor_Binding => (reference (cpu0))
			applies to
			input,circular,alpha,output;
	end kwic_sys.i;
	
	-- Mimic user input to the system
	process input_pr
		features
			lines_out : out data port;
		flows
			fi: flow source lines_out;
	end input_pr;
	
	process implementation input_pr.i
		subcomponents
			thr : thread input_th;
		connections
			c0 : port thr.lines_out -> lines_out;
		flows
			fi: flow source thr.fi -> c0 -> lines_out;	
	end input_pr.i;
	
	process circular_pr
		features
			lines_in : in data port;
			lines_out : out data port;
		flows
			fc : flow path lines_in -> lines_out;
	end circular_pr;
	
	process implementation circular_pr.i
		subcomponents
			thr : thread circular_th;
		connections
			c0 : port thr.lines_out -> lines_out;
			c1 : port lines_in -> thr.lines_in;
		flows
			fc : flow path lines_in -> c1 -> thr.fc -> c0 -> lines_out;
	end circular_pr.i;
	
	process alpha_pr
		features
			lines_in : in data port;
			lines_out : out data port;
		flows
			fa : flow path lines_in -> lines_out;
	end alpha_pr;
	
	process implementation alpha_pr.i
		subcomponents
			thr : thread alpha_th;
		connections
			c0 : port thr.lines_out -> lines_out;
			c1 : port lines_in -> thr.lines_in;
		flows
			fa : flow path lines_in -> c1 -> thr.fa -> c0 -> lines_out;
	end alpha_pr.i;
	
	process output_pr
		features
			lines_in : in data port;
		flows
			fo : flow sink lines_in;
	end output_pr;
	
	process implementation output_pr.i
		subcomponents
			thr : thread output_th;
		connections
			c0 : port lines_in -> thr.lines_in;
		flows
			fo : flow sink lines_in -> c0 -> thr.fo;
	end output_pr.i;
	
	thread input_th
		features
			lines_out : out data port;
		flows
			fi : flow source lines_out;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 1ms..3ms;
	end input_th;
	
	thread circular_th
		features
			lines_in : in data port;
			lines_out : out data port;
		flows
			fc : flow path lines_in -> lines_out;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 2ms..8ms;
	end circular_th;
	
	thread alpha_th
		features
			lines_in : in data port;
			lines_out : out data port;
		flows
			fa : flow path lines_in -> lines_out;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 2ms..10ms;
	end alpha_th;
	
	thread output_th
		features
			lines_in : in data port;
		flows
			fo : flow sink lines_in;
		properties
			Dispatch_Protocol => Sporadic;
			Compute_Execution_Time => 1ms..3ms;
	end output_th;
	
	processor genuineintel
	end genuineintel;
	
	memory mem
	end mem;
	
end kwic;